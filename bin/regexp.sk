type symb
type lett
type word

val lett_of_symb : symb → lett
val equal : (symb, lett) → ()

val eps : word
val isempty : word → ()
val head : word → lett
val tail : word → word
val cons : (lett, word) → word


val cut_dot (w: word) : (word, word) =
  branch
    (eps, w)
  or
    let l =@ head w in
    let t =@ tail w in
    let (t1, t2) =@ cut_dot t in
    let t1' = cons (l, t1) in
    (t1', t2)
  end

val cut_star (w: word) : (word, word) =
  let l =@ head w in
  let t =@ tail w in
  let (t1, t2) =@ cut_dot t in
  let t1' = cons (l, t1) in
  (t1', t2)


type expr =
  | Empty
  | Symb symb
  | Plus (expr, expr)
  | Dot (expr, expr)
  | Star expr

type m<a> = a

val return<a> (v: a) : m<a> = v
val ret (v: ()): m<()> = return<()> v

val bind<a, b> ((w: m<a>), (f: a → m<b>)) : m<b> =
  f w

type result = m<()>

binder @ = bind

val eval ((e: expr), (w: word)) : result =
  branch
    let Symb s =@ e in
    let l =@ head w in
    let t =@ tail w in
    let _ =@ isempty t in
    let _ =@ equal (s, l) in
    ret ()
  or
    let Plus (e1, _) =@ e in
    let _ =@ eval (e1, w) in
    ret ()
  or
    let Plus (_, e2) =@ e in
    let _ =@ eval (e2, w) in
    ret ()
  or
    let Dot (e1, e2) =@ e in
    let (w1, w2) =@ cut_dot w in
    let _ =@ eval (e1, w1) in
    let _ =@ eval (e2, w2) in
    ret ()
  or
    let Star _ =@ e in
    let _ = isempty w in
    ret ()
  or
    let Star e1 =@ e in
    let (w1, w2) =@ cut_star w in
    let _ =@ eval (e1, w1) in
    let _ =@ eval (e, w2) in
    ret ()
  end